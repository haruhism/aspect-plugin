plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.gradle.plugin-publish'
}

group = LIB_GROUP_ID
version = LIB_VERSION_NAME

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    //gradle sdk
    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:7.3.1'
}

gradlePlugin {
    website = LIB_WEBSITE
    vcsUrl = LIB_VCS_URL

    plugins {
        create("aspectPlugin") {
            id = LIB_PLUGIN_ID
            displayName = LIB_DISPLAY_NAME
            description = LIB_DESCRIPTION
            implementationClass = 'com.crossgate.plugin.aspect.AspectGradlePlugin'
            tags.set(['android', 'assemble', 'kotlin'])
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = LIB_GROUP_ID
            artifactId = LIB_ARTIFACT_ID
            version = LIB_VERSION_NAME

            pom {
                name = LIB_DISPLAY_NAME
                description = LIB_DESCRIPTION
                url = LIB_WEBSITE
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'haruhism'
                        name = 'nil'
                        email = 'zhangsea0417@gmail.com'
                    }
                }
                scm {
                    url = LIB_WEBSITE
                    connection = LIB_CONNECTION
                    developerConnection = LIB_DEV_CONNECTION
                }
            }

            from components.java
        }
    }
}
